plugins {
	id 'java'
	id 'jacoco'
	id 'org.springframework.boot' version '2.7.11'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

group = 'com.task'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// 직접추가
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation group: 'com.auth0', name: 'java-jwt', version: '4.3.0'
	implementation group: 'org.qlrm', name: 'qlrm', version: '2.1.1'

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'
	implementation 'io.jsonwebtoken:jjwt:0.9.1'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'junit:junit'

}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy 'jacocoTestReport'
}

jacoco {
	toolVersion = '0.8.5'
}

jacocoTestReport {
	reports {
		html.enabled true
		csv.enabled false
		xml.enabled false
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it,
					exclude: ["com/task/needmoretask/core/**",
							  "com/task/needmoretask/dto/**",
							  "com/task/needmoretask/NeedMoreTaskApplication.class"])
		}))
	}
	finalizedBy 'jacocoTestCoverageVerification'

}

jacocoTestCoverageVerification {
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it,
					exclude: ["com/task/needmoretask/core/**",
							  "com/task/needmoretask/dto/**",
							  "com/task/needmoretask/NeedMoreTaskApplication.class"])
		}))
	}
	violationRules{
		rule {
			enabled = true //true로 바꾸기
			element = 'CLASS'

			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.80
			}

			limit {
				counter = 'BRANCH'
				value = 'COVEREDRATIO'
				minimum = 0.80
			}

			excludes = ["com/task/needmoretask/core/**",
						"com/task/needmoretask/dto/**"]
		}
	}
}

